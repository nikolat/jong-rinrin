//******************************************************************************
/*
SHIORI/3.0制御スクリプト for YAYA
original written by umeici.
履歴
ver. date     note
1.0  ?        yaya向けに書き換え by C.Ponapalt
1.1  20070428 config分離、READFMOを使ったルーチン追加
1.2  20080323 構造変更（shiori3,config,compatible,optional)
1.2c 20081015 自己流カスタマイズ by Don
1.3  20210701 辞書読み込みエラーが起きた時の緊急フォールバック機能対応
*/
//******************************************************************************

#define	C_CRLF2		C_CRLF+C_CRLF		/* 改行コードx2   */
#define	C_CRLF		CHR(0xd)+CHR(0xa)	/* 改行コード     */

load   { foreach GETFUNCLIST('GOYA.OnSystemLoad')  ; _load   { EVAL(_load)   } }
unload { foreach GETFUNCLIST('GOYA.OnSystemUnload'); _unload { EVAL(_unload) } }
request
{
	_reqdata   = _argv[0]
	_linestart = 0
	_lineend   = STRSTR(_reqdata, C_CRLF, _linestart)

	if _lineend <= 0 {
		//1行目すらない！
		"SHIORI/3.0 400 Bad Request%(C_CRLF)Charset: %(S_CHARSET)%(C_CRLF2)"
		return
	}
	_lin = SUBSTR(_reqdata,_linestart,(_lineend - _linestart))

	// リクエスト種別とプロトコル名の取得　エラーなら400
	_command  = _lin[0," SHIORI"]

	// リクエストヘッダの取得
	var.req.key   = IARRAY
	var.req.value = IARRAY
	var.req.rawvalue = IARRAY

	_id = ''
	status = ''

	while _lineend > _linestart {
		//行分割
		_linestart = _lineend + 2
		_lineend = STRSTR(_reqdata, C_CRLF, _linestart)
		
		//空行もしくはみつからなかった
		if _lineend <= _linestart {
			break
		}

		_lin = SUBSTR(_reqdata,_linestart,(_lineend - _linestart))

		// キーと値を取得
		_len = STRLEN(_lin)
		_pos = STRSTR(_lin,": ",0)
		var.req.key ,= (_key = SUBSTR(_lin,0,_pos))
		_value       = SUBSTR(_lin,(_pos + 2),(_len - _pos - 2))

		if var.req.key == '' {
			break
		}

		// イベントID名称を取得
		if _key == 'Charset' {
			if S_CHARSET != _value {
				void SETSETTING('charset.output',_value)
				S_CHARSET = _value
			}
		}
		elseif _key == 'ID' {
			_id = _value

			// ハンドラが無い場合は即返る
			if !GOYA.IsImportantEvent(_id) {
				if !ISFUNC(GOYA.MakeEventHandler(_id,_command)) {
					GOYA.MakeEmptyResponse(_id) + C_CRLF
					return
				}
			}
		}
		// セキュリティレベル="External"なら即返る
		elseif _key == 'SecurityLevel' {
			if _value == 'External' {
				"SHIORI/3.0 204 No Content%(C_CRLF)Charset: %(S_CHARSET)%(C_CRLF2)"
				return
			}
		}
		// Sender取得
		elseif _key == 'Sender' {
			sender = _value
		}
		// Status取得
		elseif _key == 'Status' {
			status = _value
		}
		// キーと値を記憶
		var.req.rawvalue ,= _value
		var.req.value ,= TOAUTOEX(_value)
	}
	GOYA.OnRequest(_id,_command)
}

TOAUTOEX
{
	_v = _argv[0]
	if TOSTR(TOAUTO(_v)) == _v {
		TOAUTO(_v)
		return
	}
	_v
}

//******************************************************************************
// initializing global variable
//******************************************************************************
GOYA.OnSystemLoad.GOYA.CORE
{
	// ベースウェア通信用変数
	var.req.key   = (IARRAY, 'Path')
	var.req.value = (IARRAY, _argv[0])
	var.req.rawvalue = (IARRAY,_argv[0])
	sender        = ''
	status        = ''
	reference     = IARRAY
	res_reference = IARRAY
	marker        = ''
	S_CHARSET     = GETSETTING('charset.output')
	GOYA.ErrorDescription = ''
	GOYA.ErrorLevel = ''
	GOYA.LastErrorDescription = ''
	GOYA.InitAdditionalReturn()

	// 時系列変数
	ghostboottime = systemuptime()

	// ランダムトーク用変数
	GOYA.AiTalkCount = 0

	// SAORI用変数
	SAORI.DllList  = IARRAY
	valueex        = IARRAY
	LIB.PROTOCOL   = ''
	LIB.STATUSCODE = ''
	var.lib.key    = IARRAY
	var.lib.value  = IARRAY
	var.lib.result = ''
}
GOYA.OnSystemUnload.GOYA.CORE
{
	// ベースウェア通信用変数
	ERASEVAR('var.req.key')
	ERASEVAR('var.req.value')
	ERASEVAR('var.req.rawvalue')
	ERASEVAR('sender')
	ERASEVAR('status')
	ERASEVAR('reference')
	ERASEVAR('res_reference')
	ERASEVAR('marker')
	ERASEVAR('S_CHARSET')
	ERASEVAR('GOYA.ErrorDescription')
	ERASEVAR('GOYA.ErrorLevel')
	ERASEVAR('GOYA.LastErrorDescription')
	GOYA.ClearAdditionalReturnVariable()

	// 時系列変数
	ERASEVAR('ghostboottime')

	// ランダムトーク用変数
	ERASEVAR('GOYA.AiTalkCount')

	// SAORI用変数
	ERASEVAR('SAORI.DllList')
	ERASEVAR('valueex')
	ERASEVAR('LIB.PROTOCOL'  )
	ERASEVAR('LIB.STATUSCODE')
	ERASEVAR('var.lib.key'   )
	ERASEVAR('var.lib.value' )
	ERASEVAR('var.lib.result')
}

//******************************************************************************
// system functions
//******************************************************************************
//------------------------------------------------------------------------------
//関数名：GOYA.OnRequest
//機能　：受け取ったリクエストに応じた処理を行います
//引数　：_argv[0] EventID
//　　　：_argv[1] RequestCommand
//------------------------------------------------------------------------------
GOYA.OnRequest
{
	_id      = _argv[0]
	_command = _argv[1]
	_result  = ''
	// コマンド別に処理分岐
	case _command {
	when 'NOTIFY' {
		// NOTIFYリクエスト
		_result = GOYA.RaiseIDEvent('notify.' + _id)
		"SHIORI/3.0 200 OK%(C_CRLF)Charset: %(S_CHARSET)%(C_CRLF)"
	}
	when 'GET' {
		// GETリクエスト
		if SUBSTR(_id, 0, 2) == 'On' {
			_result = GOYA.RaiseIDEvent('event.' + _id)
		}
		else {
			_result = GOYA.RaiseIDEvent('resource.' + _id)
		}
		if _result == '' {
			GOYA.MakeEmptyResponse(_id)
		}
		else {
			"SHIORI/3.0 200 OK%(C_CRLF)Sender: AYA%(C_CRLF)Charset: %(S_CHARSET)%(C_CRLF)/
				Value: %(_result)%(C_CRLF)"
			--
			GOYA.AssembleReferenceHeader()

			GOYA.GetLastErrorLog()
			
			if GOYA.LastErrorDescription != GOYA.ErrorDescription {
				GOYA.LastErrorDescription = GOYA.ErrorDescription
				
				GOYA.PushAdditionalReturn('ErrorLevel',GOYA.ErrorLevel)
				GOYA.PushAdditionalReturn('ErrorDescription',GOYA.ErrorDescription)
			}
		}
	}
	others
		// 未知のリクエスト
		"SHIORI/3.0 400 Bad Request%(C_CRLF)"
	}
	--
	GOYA.GetAdditionalReturns()
	--
	C_CRLF
}

//------------------------------------------------------------------------------
//関数名：GOYA.MakeEventHandler
//機能　：EventIDの前に'event.'/'resource.'/'notify.'をつけたものを返す
//引数　：_argv[0] EventID
//　　　：_argv[1] RequestCommand
//------------------------------------------------------------------------------
GOYA.MakeEventHandler
{
	_id      = _argv[0]
	_command = _argv[1]
	_ret     = ''
	if _command == 'NOTIFY' {
		_ret = 'notify.' + _id
	} elseif _command == 'GET' {
		if SUBSTR(_id, 0, 2) == 'On' {
			_ret = 'event.' + _id
		} else {
			_ret = 'resource.' + _id
		}
	}
	_ret
}

//------------------------------------------------------------------------------
//関数名：GOYA.IsImportantEvent
//機能　：絶対に通知すべきイベントを識別します
//引数　：_argv[0] EventID
//------------------------------------------------------------------------------
GOYA.IsImportantEvent
{
	case _argv[0] {
	when 'OnSecondChange','OnUpdateReady','OnUpdateComplete'
		1
	}
	0
}

//------------------------------------------------------------------------------
//関数名：GOYA.MakeEmptyResponse
//機能　："204を返してはいけないリクエスト"を判定して、
//        適正な応答文字列を作成します
//引数　：_argv[0] EventID
//------------------------------------------------------------------------------
GOYA.MakeEmptyResponse
{
	case _argv[0] {
	when 'OnFirstBoot', 'OnBoot', 'OnWindowStateRestore', 'OnGhostChanged', 'OnShellChanged'
		// スコープ0/1の基本サーフィスを出す必要があるイベント
		"SHIORI/3.0 200 OK%(C_CRLF)Sender: AYA%(C_CRLF)Charset: %(S_CHARSET)%(C_CRLF)/
			Value: \0\s[0]\1\s[10]\e%(C_CRLF2)"
	when 'OnClose'
		// 終了指示を出す必要があるイベント
		"SHIORI/3.0 200 OK%(C_CRLF)Sender: AYA%(C_CRLF)Charset: %(S_CHARSET)%(C_CRLF)/
			Value: \0\-\e%(C_CRLF2)"
	others
		// 上記以外では204を返送して良い
		"SHIORI/3.0 204 No Content%(C_CRLF)Charset: %(S_CHARSET)%(C_CRLF)"
	}
}

//------------------------------------------------------------------------------
//関数名：GOYA.RaiseIDEvent
//機能　：指定された名前の関数を実行して結果を取得します
//引数　：_argv[0] ヘッダ + EventID
//------------------------------------------------------------------------------
GOYA.RaiseIDEvent
{
	// reference 変数を作成
	reference = IARRAY
	_sz = ARRAYSIZE(var.req.key)
	for _i = 0; _i < _sz; _i++ {
		if SUBSTR(_keyname = var.req.key[_i], 0, 9) == 'Reference' {
			_refnum = TOINT(SUBSTR(_keyname, 9, 3))
			_value = var.req.value[_i]
			reference[_refnum] = _value
		}
	}
	// res_reference 配列を初期化
	res_reference = IARRAY

	_event = _argv[0]
	
	case _id {
		when 'OnSecondChange' {
			// OnSecondChangeならランダムトーク関連処理
			GOYA.ControlAiTalk(&_event)
		}
		when 'OnUpdateReady' {
			// OnUpdateReadyならロード中のSAORIをすべてunloadする
			GOYA.SaoriUnloadAll()
		}
		when 'OnUpdateComplete' {
			// OnUpdateCompleteならdl2を探す。存在したら同名のdllを削除、dl2はdllにリネームする
			// もっともAYA自身が対象だった場合はどうしようもないが。
			GOYA.Dl2ToDll()
		}
	}

	// イベント活性化
	if ISFUNC(_event) {
		EVAL(_event)
	}

	// reference 変数をクリア
	ERASEVAR('reference')
}

//------------------------------------------------------------------------------
//関数名：GOYA.GetLastErrorLog
//機能　：エラーログに表示すべき文字列を作成します
//------------------------------------------------------------------------------
GOYA.GetLastErrorLog
{
	_errors = GETERRORLOG()
	
	_errorlevel_text = 'GOYA.ERRORLOGLEVEL'
	_errorlevel = 2
	
	if _errorlevel_text == 'note' {
		_errorlevel = 0
	}
	elseif _errorlevel_text == 'warning' {
		_errorlevel = 1
	}
	
	GOYA.ErrorDescription = ''
	GOYA.ErrorLevel = ''
	
	foreach _errors; _error {
		_el = 0
		
		if ( STRSTR(_error,'error',0) >= 0 ) {
			_el = 2
		}
		elseif ( STRSTR(_error,'warning',0) >= 0 ) {
			_el = 1
		}
		
		if _el < _errorlevel {
			continue
		}
		
		if STRLEN(GOYA.ErrorDescription) > 0 {
			GOYA.ErrorDescription += CHR(1)
			GOYA.ErrorLevel += CHR(1)
		}
		
		GOYA.ErrorDescription += _error
		
		if _el == 0 {
			GOYA.ErrorLevel += 'info'
		}
		elseif _el == 1 {
			GOYA.ErrorLevel += 'warning'
		}
		else {
			GOYA.ErrorLevel += 'critical'
		}
	}	
}

//------------------------------------------------------------------------------
//関数名：GOYA.InitAdditionalReturn
//機能　：追加ヘッダ用変数を初期化します
//------------------------------------------------------------------------------
GOYA.InitAdditionalReturn : void
{
	GOYA.AdditionalReturn.String = ''
}

//------------------------------------------------------------------------------
//関数名：GOYA.PushAdditionalReturn
//機能　：ヘッダを追加します (設定はリクエストごとにリセットされます)
//引数　：_argv[0] ヘッダ名  _argv[1] 値
//------------------------------------------------------------------------------
GOYA.PushAdditionalReturn : void
{
	_key="%(_argv[0]): "
	_argv[0]=IARRAY
	GOYA.AdditionalReturn.String += _key+TOSTR(_argv)+C_CRLF
}

//------------------------------------------------------------------------------
//関数名：GOYA.ClearAdditionalReturnVariable
//機能　：追加ヘッダ用変数を削除します
//------------------------------------------------------------------------------
GOYA.ClearAdditionalReturnVariable : void
{
	ERASEVAR('GOYA.AdditionalReturn.String')
}

//------------------------------------------------------------------------------
//関数名：GOYA.GetAdditionalReturns
//機能　：追加ヘッダを展開します
//返値　：追加すべきヘッダ文字列 最後は必ず改行で終わる
//------------------------------------------------------------------------------
GOYA.GetAdditionalReturns
{
	_t=GOYA.AdditionalReturn.String
	GOYA.AdditionalReturn.String = ''
	_t
}

//------------------------------------------------------------------------------
//関数名：GOYA.AssembleReferenceHeader
//機能　：汎用配列'res_reference'から返送用のReference文字列を作成します
//　　　　変数'marker'から返送用のMarker文字列を作成します
//------------------------------------------------------------------------------
GOYA.AssembleReferenceHeader
{
	_result = ''
	if ISVAR('res_reference') {
		for _i = 0; _i < ARRAYSIZE(res_reference); _i++ {
			_result += "Reference%(_i): %(res_reference[_i])%(C_CRLF)"
		}
		ERASEVAR('res_reference')
	}
	if ISVAR('marker') {
		_result += "Marker: %(marker)%(C_CRLF)"
		ERASEVAR('marker')
	}
	_result
}

//------------------------------------------------------------------------------
//関数名：GOYA.ControlAiTalk
//機能　：AIトーク発動処理
//引数　：_argv[0] イベント名
//------------------------------------------------------------------------------
GOYA.ControlAiTalk
{
	if TOINT(reference[3]) { //喋れる
		if aitalkinterval > 0 {		// トーク間隔が非0
			GOYA.AiTalkCount++

			if GOYA.AiTalkCount > aitalkinterval {	// トーク間隔の設定時間を越えた
				GOYA.AiTalkCount = 0
				_argv[0] = 'event.OnAiTalk'
			}
		}
	}
}

//******************************************************************************
//ネットワーク更新後にできた dl2 ファイルに関する処置
//******************************************************************************
//------------------------------------------------------------------------------
//関数名：GOYA.Dl2ToDll
//機能　：処理の開始
//------------------------------------------------------------------------------
GOYA.Dl2ToDll
{
	GOYA.ExecuteDl2ToDll('')
}

//------------------------------------------------------------------------------
//関数名：GOYA.ExecuteDl2ToDll
//機能　：指定したディレクトリに存在するdl2を処理
//引数　：_argv[0] 処理対象パス（AYA dllからの相対パス指定）
//------------------------------------------------------------------------------
GOYA.ExecuteDl2ToDll
{
	// 指定ディレクトリ内のファイルを列挙
	_files   = TOLOWER(FENUM(_argv[0]))
	// 拡張子dl2のファイルをリストに蓄積する　子ディレクトリ内も再帰的に処理する
	_targets = IARRAY
	foreach _files; _file {
		if SUBSTR(_file, 0, 1) == '\'
			GOYA.ExecuteDl2ToDll(_argv[0] + _file)
		else {
			_s_path = SPLITPATH(_file)
			if _s_path[3] == '.dl2'
				_targets ,= _s_path[2]
		}
	}

	// 拡張子dl2をdllへ名前変更する　旧dllは消去する
	foreach _targets; _target {
		_path = _argv[0] + '\' + _target
		_dmy = FDEL(_path + '.dll')
		_dmy = FRENAME(_path + '.dl2', _path + '.dll')
	}
}

//******************************************************************************
//文 version 3 システム関数 / システム変数の補完
//FUNCTIONEX / SAORI
//******************************************************************************
//------------------------------------------------------------------------------
//関数名：FUNCTIONLOAD
//機能　：SAORIを読み込みます
//引数　：_argv[0]　 対象DLL名
//------------------------------------------------------------------------------
FUNCTIONLOAD
{
	// load　エラーなら抜ける　初回のloadならGET Versionを送出する
	_r_load = LOADLIB(_argv[0])
	if !_r_load {
		0
		return
	}
	if _r_load == 1 {
		if SUBSTR(GOYA.SendGETVersion(_argv[0]), 0, 1) != '2' {
			UNLOADLIB(_argv[0])
			0
			return
		}
		SAORI.DllList ,= _argv[0]
	}
	1
}

//------------------------------------------------------------------------------
//関数名：FUNCTIONEX
//機能　：SAORIを実行します
//引数　：_argv[0]　 対象DLL名
//　　　　_argv[1]～ Argument0～
//------------------------------------------------------------------------------
FUNCTIONEX
{
	if FUNCTIONLOAD(_argv[0]) == 0 {
		0
		return
	}

	// リクエスト文字列を作成
	_reqheader = /
		"EXECUTE SAORI/1.0%(C_CRLF)/
		Charset: %(CHARSETLIBEX(_argv[0]))%(C_CRLF)/
		Sender: AYA%(C_CRLF)/
		SecurityLevel: Local%(C_CRLF)"
	for _i = 1; _i < _argc; _i++ {
		_reqheader += "Argument%(_i - 1): %(_argv[_i])%(C_CRLF)"
	}
	_reqheader += C_CRLF

	// 実行
	GOYA.RequestLib(_argv[0], _reqheader)

	if var.lib.result != ''
		var.lib.result
	GOYA.MakeValueexVariable
}

//------------------------------------------------------------------------------
//関数名：SAORI
//機能　：SAORIを実行します
//        FUNCTIONEXのシノニム。一段下駄が入る分FUNCTIONEXより低速になりますが、
//        気にするほどではありません。
//------------------------------------------------------------------------------
SAORI { FUNCTIONEX(_argv) }

//------------------------------------------------------------------------------
//関数名：GOYA.SendGETVersion
//機能　：SAORIへGET Versionを送出します
//引数　：_argv[0] SAORIファイル名
//------------------------------------------------------------------------------
GOYA.SendGETVersion
{
	GOYA.RequestLib(_argv[0], "GET Version SAORI/1.0%(C_CRLF)Charset: %(CHARSETLIBEX(_argv[0]))%(C_CRLF)Sender: AYA%(C_CRLF2)")
	LIB.STATUSCODE
}

//------------------------------------------------------------------------------
//関数名：GOYA.RequestLib
//機能　：SAORIへリクエストを送出し、結果を得ます
//引数　：_argv[0] SAORIファイル名
//　　　　_argv[1] リクエスト文字列
//------------------------------------------------------------------------------
GOYA.RequestLib
{
	LIB.PROTOCOL   = ''
	LIB.STATUSCODE = ''
	var.lib.key    = IARRAY
	var.lib.value  = IARRAY

	// リクエスト送出
	_result = REQUESTLIB(_argv[0], _argv[1])

	// 結果の解析

	// 改行で分割
	_lines = RE_SPLIT(_result, C_CRLF)

	// プロトコル名と結果の取得
	_seppos = STRSTR(_lines[0], ' ', 0)
	if _seppos == -1; return
	LIB.PROTOCOL   = SUBSTR(_lines[0], 0, _seppos)
	LIB.STATUSCODE = SUBSTR(_lines[0], _seppos + 1, STRLEN(_lines[0]) - _seppos - 1)

	// レスポンスヘッダの取得
	var.lib.key   = IARRAY
	var.lib.value = IARRAY
	var.lib.result = ''
	_skip = 1
	foreach _lines; _lin {
		// 1行目は飛ばす
		if _skip {
			_skip = 0
			continue
		}
		// キーと値を取得
		_len         = STRLEN(_lin)
		_pos         = STRSTR(_lin, ': ', 0)
		var.lib.key ,= (_key = SUBSTR(_lin, 0, _pos))
		_value       = SUBSTR(_lin, _pos + 2, _len - _pos - 2)
		_valuecv     = TOAUTO(_value)
		
		var.lib.value ,= _valuecv
		if _key == 'Result' {
			var.lib.result = _valuecv
		}
	}
}

//------------------------------------------------------------------------------
//関数名：GOYA.MakeValueexVariable
//機能　：リクエストヘッダからvalueex配列を作成します
//------------------------------------------------------------------------------
GOYA.MakeValueexVariable
{
	valueex = IARRAY
	for _i = 0; _i < ARRAYSIZE(var.lib.key); _i++ {
		if SUBSTR(_keyname = var.lib.key[_i], 0, 5) == 'Value' {
			valueex[TOINT(SUBSTR(_keyname, 5, 3))] = var.lib.value[_i]
		}
	}
}

//------------------------------------------------------------------------------
//関数名：GOYA.SaoriUnloadAll
//機能　：ロードされているSAORIをすべてunloadします
//------------------------------------------------------------------------------
GOYA.SaoriUnloadAll
{
	foreach SAORI.DllList; _saori { UNLOADLIB(_saori) }
	SAORI.DllList = IARRAY
}

//------------------------------------------------------------------------------
//　関数名：GOYA.CanTalk
//　機能　：Statusヘッダからしゃべれるかどうかを判定する
//------------------------------------------------------------------------------
GOYA.CanTalk
{
	if 'talking' _in_ status {
		0
		return
	}
	if 'choosing' _in_ status {
		0
		return
	}
	if 'minimizing' _in_ status {
		0
		return
	}
	if 'timecritical' _in_ status {
		0
		return
	}
	
	1
	return
}

//******************************************************************************
//配列操作
//******************************************************************************
JOIN
{
	if _argc <= 2 {
		_argv[0]
		return
	}
	
	_delim = _argv[_argc - 1]
	_argc--
	
	_text = _argv[0]
	for _i = 1 ; _i < _argc ; _i++ {
		_text += _delim
		_text += _argv[_i]
	}
	_text
}

REVERSE
{
	_a = IARRAY
	for _i = 0 ; _i < _argc ; _i++ {
		_a ,= _argv[_argc-_i-1]
	}
	_a
}

UNIQUE
{
	_v = _argv
	_n = ARRAYSIZE(_v)
	
	for _i = 0 ; _i < _n ; _i++ {
		for _j = _i+1 ; _j < _n ; _j++ {
			if _v[_i] == _v[_j] {
				_v[_j] = IARRAY
				_n -= 1
				_j -= 1
			}
		}
	}
	_v
}

SPLITEX
{
	_a = SPLIT(_argv)
	_n = ARRAYSIZE(_a)
	for _i = _n-1 ; _i >= 0 ; _i-- {
		if _a[_i] == '' {
			_a[_i] = IARRAY
		}
	}
	_a
}

MAX
{
	_v = _argv[0]
	for _i = 1 ; _i < _argc ; _i++ {
		if _v < _argv[_i] {
			_v = _argv[_i]
		}
	}
	_v
}

MIN
{
	_v = _argv[0]
	for _i = 1 ; _i < _argc ; _i++ {
		if _v > _argv[_i] {
			_v = _argv[_i]
		}
	}
	_v
}

AVERAGE
{
	_v = 0
	for _i = 0 ; _i < _argc ; _i++ {
		_v += _argv[_i]
	}
	_v / _argc
}

//******************************************************************************
// 時刻系変数
//******************************************************************************
year    { GETTIME[0] }
month   { GETTIME[1] }
day     { GETTIME[2] }
weekday { GETTIME[3] }
hour    { GETTIME[4] }
ampm    { if hour >= 12; 1;   else; 0    }
hour12  { if ampm; hour - 12; else; hour }
minute  { GETTIME[5] }
second  { GETTIME[6] }

systemuptickcount { GETTICKCOUNT }
systemuptime
{
	_highcount = GETTICKCOUNT(1)
	if _highcount > 1
		GETTICKCOUNT/1000
	else
		(_highcount*0x40000000 + (GETTICKCOUNT/2))/500
}

systemupsecond    { SHIORI3FW.GetTickInfo[3] }
systemupminute    { SHIORI3FW.GetTickInfo[1] }
systemuphour      { SHIORI3FW.GetTickInfo[2] }

SHIORI3FW.GetTickInfo
{
	_result     = (IARRAY, systemuptime)
	_result    ,= _result[0]/60
	_result    ,= _result[1]/60
	_result    ,= _result[0] - _result[1]*60
	_result[1] -= _result[2]*60

	_result
}

ghostupmin
{
	(GETSECCOUNT() / 60) - ghostbootmin
}

ghostupmin_total
{
	ghostupmin_last + ghostupmin()
}

//******************************************************************************
// メモリ系変数
//******************************************************************************
memoryload         { GETMEMINFO[0] }
memorytotalphys    { GETMEMINFO[1] }
memoryavailphys    { GETMEMINFO[2] }
memorytotalvirtual { GETMEMINFO[3] }
memoryavailvirtual { GETMEMINFO[4] }
